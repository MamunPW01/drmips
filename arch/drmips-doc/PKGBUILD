# Maintainer: Bruno Nova <brunomb.nova@gmail.com>
pkgname=drmips-doc
_pkgname=drmips
pkgver=1.2.2
pkgrel=1
pkgdesc="Graphical MIPS simulator to support computer architecture teaching and learning - User Manuals"
arch=('any')
url="https://bitbucket.org/brunonova/drmips"
license=('GPL3')
makedepends=('cmake' 'texlive-core' 'texlive-latexextra' 'texlive-science')
optdepends=('drmips: the simulator')
source=("https://bitbucket.org/brunonova/$_pkgname/downloads/DrMIPS_v$pkgver.tar.gz"
        "only_compile_manuals.patch")
md5sums=('499bc462d6b9aa3367fd7d41fa2bdd24'
         'c95c0e47f5ba91d42cc8073aa81c5fd6')

prepare() {
	cd "$srcdir"
	patch -p1 -i "$srcdir/only_compile_manuals.patch"
}

build() {
	cd "$srcdir"
	cmake . -DCMAKE_INSTALL_PREFIX=/usr -DDRMIPS_BUILD_PC_VERSION=off
	make
}

package() {
	cd "$srcdir"
	make DESTDIR="$pkgdir" install
	mkdir -p "$pkgdir/usr/share/doc/$_pkgname"
	ln -s "/usr/share/$_pkgname/doc" "$pkgdir/usr/share/doc/$_pkgname/manuals"
}
