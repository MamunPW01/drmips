# Maintainer: Bruno Nova <brunomb.nova@gmail.com>
pkgname=drmips-nodoc
_pkgname=drmips
pkgver=1.2.0
pkgrel=1
pkgdesc="Graphical MIPS simulator to support computer architecture teaching and learning - This package doesn't include the documentation"
arch=('any')
url="https://bitbucket.org/brunonova/drmips"
license=('GPL3')
depends=('java-environment' 'desktop-file-utils')
makedepends=('jdk7-openjdk' 'cmake')
conflicts=('drmips')
install="$pkgname.install"
source=("https://bitbucket.org/brunonova/$_pkgname/downloads/DrMIPS_v$pkgver.tar.gz"
        "https://bitbucket.org/brunonova/$_pkgname/raw/v$pkgver/debian/patches/adapt_usage_help_text.patch"
        "https://bitbucket.org/brunonova/$_pkgname/raw/v$pkgver/debian/$_pkgname.desktop"
        "https://bitbucket.org/brunonova/$_pkgname/raw/v$pkgver/debian/$_pkgname.1"
        "https://bitbucket.org/brunonova/$_pkgname/raw/v$pkgver/debian/$_pkgname.pt.1")
md5sums=('999640ef041551e141a74aab64ecd2fe'
         '4789f9db632ba230b182271a8ec581c2'
         '02ba7efa585b621e978f6a23e8fd210e'
         '4910bafb9087b68ed9fa40bfd46838c5'
         '70954b479c35eeaff5f9306d0ec5eff9')

prepare() {
	cd "$srcdir"
	patch -p1 -i "adapt_usage_help_text.patch"
}

build() {
	cd "$srcdir"
	cmake . -DCMAKE_INSTALL_PREFIX=/usr -DDRMIPS_BUILD_MANUALS=off
	make
}

package() {
	cd "$srcdir"
	make DESTDIR="$pkgdir" install
	install -Dm644 "icons/$_pkgname.png" "$pkgdir/usr/share/pixmaps/$_pkgname.png"
	install -Dm644 "$_pkgname.desktop" "$pkgdir/usr/share/applications/$_pkgname.desktop"
	install -Dm644 "$_pkgname.1" "$pkgdir/usr/share/man/man1/$_pkgname.1"
	install -Dm644 "$_pkgname.pt.1" "$pkgdir/usr/share/man/pt/man1/$_pkgname.1"
}
